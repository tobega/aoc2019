include 'lib/hash'
include 'lib/list'

data col <"x">, row <"y">

def map: 1"y":[ $IN::lines -> 1"x":[ $... ]];

def start: $map -> \[i;j](<='@'> { row: $i, col: $j } !\) ... ...;

def keyPositions: [ $map -> \[i;j](<'\p{Lower}'> { key: $, row: $i, col: $j } !\) ... ...];

def keys: [ $keyPositions... ->  $.key::raw ];

def doors: [ $map -> \[i;j](<'\p{Upper}'> $ !\) ... ...];

templates calculateDistances&{map:}
  data key, at <{col: <col>, row: <row>}> local
  def directions: [ { dRow: 0"y", dCol: -1"x"}, { dRow: 0"y", dCol: 1"x"}, { dRow: -1"y", dCol: 0"x"}, { dRow: 1"y", dCol: 0"x"}];
  @: { map: $map, next: [], current: [{at: $, distance: 0"steps", doors: [], others: []}], found: [] -> hash/Map&{key: :(key:), value: :(route:)}};
  @.map($.row;$.col): '#';
  1 -> #
  $@.found !

  <?($@.current <[](1..)>)>
    def base: ^@.current(1);
    $directions...
      -> { at: { row: $base.at.row + $.dRow, col: $base.at.col + $.dCol},
           distance: $base.distance + 1"steps", doors: $base.doors,
           others: $base.others}
      -> \isValid(
         <?($@calculateDistances.map($.at.row; $.at.col) <~='#'>)>
           $! \isValid)
      -> \checkDoor( def onMap: $@calculateDistances.map($.at.row; $.at.col);
         $ -> #
         <?($doors <[<=$onMap>]>)> { $..., doors: [$.doors..., $onMap] } !
         <?($keys <[<=$onMap>]>)> { $..., others: [$.others..., $onMap] } !
         <> $!
      \checkDoor)
      -> \propagate(
          ..|@calculateDistances.next: $;
          $ ! \propagate)
      -> \update( def onMap: $@calculateDistances.map($.at.row; $.at.col);
        $ -> #
        <?($keys <[<=$onMap>]>)>
          { key: $onMap, route: {distance: $.distance, doors: $.doors, others: $.others}}
          // re-putting the first (shortest) path, if one exists
          -> @calculateDistances.found::put -> @calculateDistances.found::put -> !VOID
        <>
          @calculateDistances.map($.at.row; $.at.col): '#';
        \update)
      -> !VOID
    1 -> #
  <?($@.next <[](1..)>)>
    @.current: $@.next;
    @.next: [];
    1 -> #
end calculateDistances

processor DistanceTable
  data leg <{from: <key>, to: <key>}> local
  @: [] -> hash/Map&{key: :(leg:), value: :(route:)};

  sink add&{from:}
    def entry: { leg: { from: $from, to : $.key}, route: $.route};
    $entry -> @DistanceTable::put -> 'Error: duplicate key for $;$#10;' -> !OUT::write
  end add

  templates pathLength
    data at <key> local
    def path: $;
    @: {at: '@', length: 0"steps"};
    1..$path::length -> #
    $@.length !

    <> def i: $;
      {from: $@.at, to: $path($i)} -> @DistanceTable::get
      -> \distance( @: $.distance;
        $.doors... -> \(<?($path(1..$i) <~[<'(?i)$;'>]>)>
           @distance: $@distance + 1000"steps"; \) -> !VOID
        $.others... -> \(<?($path(1..$i) <~[<'(?i)$;'>]>)>
           @distance: $@distance + 1000"steps"; \) -> !VOID
        $@! \distance)
      -> @.length: $@.length + $;
      @.at: $path($i);
  end pathLength

  templates fourBotPathLength
    data at <[<'.'|key>=4]> local
    def path: $;
    @: {at: ['1', '2', '3', '4'], length: 0"steps"};
    1..$path::length -> #
    $@.length !

    <> def i: $;
      [$@.at... -> [{from: $, to: $path($i)} -> @DistanceTable::get]]
      -> \[k](<~=[]> @fourBotPathLength.at($k): $path($i); $!\) -> $(1) -> $(1)
      -> \distance( @: $.distance;
        $.doors... -> \(<?($path(1..$i) <~[<'(?i)$;'>]>)>
           @distance: $@distance + 1000"steps"; \) -> !VOID
        $.others... -> \(<?($path(1..$i) <~[<'(?i)$;'>]>)>
           @distance: $@distance + 1000"steps"; \) -> !VOID
        $@! \distance)
      -> @.length: $@.length + $;
  end fourBotPathLength

  source entries
    $@DistanceTable::entries!
  end entries
end DistanceTable

def distanceTable: $DistanceTable;

$start -> calculateDistances&{map: $map} -> $::entries -> !distanceTable::add&{from: '@'}
$keyPositions... -> \( def from: $.key;
  $ -> calculateDistances&{map: $map} -> $::entries -> !distanceTable::add&{from: $from}
\) -> !VOID

templates shuffle
  @: $;
  [ 1..$::length -> \($@shuffle::length -> SYS::randomInt -> ^@shuffle($+1) !\) ] !
end shuffle

source geneSoup&{popSize:, genes:, valueFn:}
  source createCell
    def values: [ $... -> valueFn ];
    $ -> \[i](<?($values($i) <..$@geneSoup.best>)> @geneSoup.best: $values($i); @geneSoup.bestGene: $;\) -> !VOID
    { age: 0, chromosome: $, value: $values -> list/min} !
  end createCell

  templates getGamete
    def which: 2 -> SYS::randomInt;
    def crossover: $genes::length + 1 -> SYS::randomInt;
    @: $.chromosome(1 + $which; 1..$crossover);
    $.chromosome(2 - $which)... -> \(<?($@getGamete <~[<=$>]>)> ..|@getGamete: $;\) -> !VOID
    $@ !
  end getGamete

  templates mate
    @: { adults: $, babies: []};
    1 -> #
    $@.babies!

    <?($@.adults <[](2..)>)>
      def pair: [ 1..2 ->
        \($@mate.adults::length -> SYS::randomInt -> ^@mate.adults($+1) ! \)];
      1..4 -> [ $pair...  -> getGamete] -> createCell -> ..|@.babies: $;
      1 -> #
  end mate

  sink updateBest
    <..~$@geneSoup.best> @geneSoup.best: $;
  end updateBest

  source isStagnated
    @: 0;
    [1..3 -> $@geneSoup.pop::length -> SYS::randomInt -> $@geneSoup.pop($+1).value ] -> #
    $@!
    
    <[<=$@geneSoup.best>+ VOID]> @: 1;
      'Stagnated at $(1);$#10;' -> !OUT::write
  end isStagnated

  templates mutate
        @: $;
        1..2 -> 20 -> SYS::randomInt -> \(<=0> $!\)
        -> $genes::length -> SYS::randomInt -> $+1
        -> \(
          def a: $ -> SYS::randomInt -> $+1;
          def b: $;
          def which: 2 -> SYS::randomInt -> $+1;
          @mutate.chromosome($which): $@mutate.chromosome($which)
          -> [$(first..$a-1), $($a..$b-1), $($b..last)] -> shuffle -> list/flatten;
        \) -> !VOID
        1..5 -> 20 -> SYS::randomInt -> \(<=0> $!\)
        -> \(
          def which: 2 -> SYS::randomInt -> $+1;
          def a: $genes::length -> SYS::randomInt -> $+1;
          def b: $genes::length -> SYS::randomInt -> $;
          def popped: ^@mutate.chromosome($which; $a);
          @mutate.chromosome($which): $@mutate.chromosome($which)
          -> [$(first..$b)..., $popped, $($b+1..last)...];
        \) -> !VOID
        $@!
  end mutate

  sink enterPop
    {tries: 5"1", candidate: $} -> #
    <?($@geneSoup.pop::length <..~$popSize>)> ..|@geneSoup.pop: $.candidate -> mutate;
    <{tries: <1"1"..>}> def toReplace: $@geneSoup.pop::length -> SYS::randomInt -> $+1;
      $ -> \(<{candidate:<{value: <..$@geneSoup.pop($toReplace).value>}>}> @geneSoup.pop($toReplace): $.candidate -> mutate;
        <> { tries: $.tries-1"1", candidate: $.candidate} ! \) -> #
  end enterPop

  @: {count: 0"1", best: 99999999999"steps", bestGene:[]};
  @.pop: [ 1..$popSize -> [ $genes -> shuffle, $genes -> shuffle ] -> createCell ];
  0 -> #

  <=1> [$@.best, $@.bestGene] !
  <>
    @.count: $@.count + 1"1";
    def babies: $@.pop -> mate;
    $babies... -> $.value -> !updateBest
'$@.count; best $:[$@.best, $@.bestGene];$#10;' -> !OUT::write
    @.pop: []; // No adults survive
    $babies... -> !enterPop
    $isStagnated -> #
end geneSoup

// part 1
//'The best path is $geneSoup&{popSize: 200, genes:$keys, valueFn: distanceTable::pathLength};
//' -> !OUT::write

// part 2
def newMap: $map -> \[i;j](
  <?($i <=$start.row-1"y">)?($j <=$start.col-1"x">)> '1' !
  <?($i <=$start.row-1"y">)?($j <=$start.col+1"x">)> '2' !
  <?($i <=$start.row+1"y">)?($j <=$start.col-1"x">)> '3' !
  <?($i <=$start.row+1"y">)?($j <=$start.col+1"x">)> '4' !
  <?($i <=$start.row-1"y">)?($j <=$start.col>)> '#' !
  <?($i <=$start.row+1"y">)?($j <=$start.col>)> '#' !
  <?($i <=$start.row>)?($j <=$start.col-1"x">)> '#' !
  <?($i <=$start.row>)?($j <=$start.col+1"x">)> '#' !
  <?($i <=$start.row>)?($j <=$start.col>)> '#' !
  <> $ !
\);


def newTable: $DistanceTable;

{row: $start.row-1"y", col: $start.col-1"x"} -> calculateDistances&{map: $newMap} -> $::entries -> !newTable::add&{from: '1'}
{row: $start.row-1"y", col: $start.col+1"x"} -> calculateDistances&{map: $newMap} -> $::entries -> !newTable::add&{from: '2'}
{row: $start.row+1"y", col: $start.col-1"x"} -> calculateDistances&{map: $newMap} -> $::entries -> !newTable::add&{from: '3'}
{row: $start.row+1"y", col: $start.col+1"x"} -> calculateDistances&{map: $newMap} -> $::entries -> !newTable::add&{from: '4'}
$keyPositions... -> \( def from: $.key;
  $ -> calculateDistances&{map: $newMap} -> $::entries -> !newTable::add&{from: $from}
\) -> !VOID

'With four bots, the best path is $geneSoup&{popSize: 200, genes:$keys, valueFn: newTable::fourBotPathLength};
' -> !OUT::write
