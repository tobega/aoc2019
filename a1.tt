import 'lib/list'

composer readWeight
  <INT>
end readWeight

def moduleWeights: [ $IN::lines -> readWeight ];

// part 1
[ $moduleWeights... -> $ ~/ 3 - 2 ] -> list/sum -> !OUT::write

'
' -> !OUT::write

// part 2
templates fuelFuel
  @: 0;
  $ -> #
  $@ !
  when <> do
    def fuel: $ ~/ 3 - 2;
    $fuel -> \(when <0~..> do
      @fuelFuel: $@fuelFuel + $fuel;
      $fuel !
    \) -> #
end fuelFuel

[ $moduleWeights... -> fuelFuel ] -> list/sum -> !OUT::write

// Alternative code

templates fuelWeight
  $ ~/ 3 - 2 -> \(when <0~..> do $ !\) !
end fuelWeight

'
' -> !OUT::write

[ $moduleWeights... -> fuelWeight ] -> list/sum -> !OUT::write

templates selfCarryingFuel
  $ -> fuelWeight -> [ $, $ -> selfCarryingFuel]... !
end selfCarryingFuel

'
' -> !OUT::write

[ $moduleWeights... -> selfCarryingFuel ] -> list/sum -> !OUT::write
