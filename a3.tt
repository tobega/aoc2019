include 'lib/hash'
include 'lib/math'
include 'lib/list'

composer wire
  [ <wiring>+ ]
  rule wiring: { direction: <'[U,D,L,R]'>, distance: <INT"1"> } (<','>?)
end wire

//def wires: [ 'R8,U5,L5,D3' -> wire, 'U7,R6,D4,L4' -> wire ];
//$wires -> !OUT::write
def wires: [$IN::lines -> wire];

templates mapWirePositions
  @: {x: 0"1", y: 0"1"};
  $... -> #

  when <{direction: <='U'>}> do
    1"1"..$.distance -> { x: $@.x, y: $@.y + $ } !
    (y: $@.y + $.distance) -> ..|@: $;

  when <{direction: <='D'>}> do
    1"1"..$.distance -> { x: $@.x, y: $@.y - $ } !
    (y: $@.y - $.distance) -> ..|@: $;

  when <{direction: <='L'>}> do
    1"1"..$.distance -> { x: $@.x - $, y: $@.y } !
    (x: $@.x - $.distance) -> ..|@: $;

  when <{direction: <='R'>}> do
    1"1"..$.distance -> { x: $@.x + $, y: $@.y } !
    (x: $@.x + $.distance) -> ..|@: $;
end mapWirePositions

//part 1
def wire1: [] -> hash/Set;
$wires(1) -> mapWirePositions -> wire1::add -> !VOID

[ $wires(2) -> mapWirePositions -> \(when <?($ -> wire1::isMember <=1>)> do $ !\)
  -> math/manhattanDistance ] -> list/min -> !OUT::write

'
' -> !OUT::write

//part 2
def wire1Steps: [] -> hash/Map&{key: :(coordinate:), value: :(segment:)};
// The double put replaces the first value in case of multiples
[ $wires(1) -> mapWirePositions ] -> \[i]( { coordinate: $, segment: $i } -> wire1Steps::put -> wire1Steps::put !\) -> !VOID

[ $wires(2) -> mapWirePositions ] -> \[i]( $ -> wire1Steps::get -> $::raw + $i ! \) -> list/min -> !OUT::write
