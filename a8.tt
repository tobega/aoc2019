composer toInt
  <INT>
end toInt

composer decodeSpaceImageFormat@{width:, height:}
  [ <layer>+ ]
  rule layer: [ <row>=$height ]
  rule row: [ <'.'>=$width -> toInt ]
end decodeSpaceImageFormat

def message: $IN::lines -> decodeSpaceImageFormat@{width:25, height:6}; // only expect one line

templates part1
  templates spectrum
    @: [ 0..2 -> 0 ];
    $ -> #
    $@ !
    when <[]> do $... -> #
    when <> do $ + 1 -> @($): $@($) + 1;
  end spectrum
  @: $(1) -> spectrum;
  $(2..last)... -> spectrum -> #
  $@(2) * $@(3) !

  when <?($(1) <..~$@(1)>)> do @: $;
end part1

$message -> part1 -> !OUT::write
'
' -> !OUT::write

templates composeImage
  templates findColour
    def layers: $;
    1 -> #
    when <?($layers($) <=2>)> do $ + 1 -> #
    when <> do $layers($) !
  end findColour
  def layers: $;
  $layers(1) -> \[row]($ -> \[col]($layers(1..last;$row;$col) -> findColour !\) !\) !
end composeImage

$message -> composeImage... ->
'$... -> \(when <=0> do '░' ! when <=1> do '█' !\);
' -> !OUT::write
