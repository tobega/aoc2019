include 'lib/hash'

data amount <"1">
data deficit <"1">
data surplus <"1">

composer parseReaction
  { ingredients: [ <ingredient>+ ], (<' => '>) result: <chemical> }
  rule chemical: { amount: <INT>, (<WS>) substance: <~'[\s,]'> }
  rule ingredient: <chemical> (<', '>?)
end parseReaction

def reactionMap: [ $IN::lines -> parseReaction ] -> hash/Map&{key: :(result:; substance:), value: :()};

templates convertToIngredients&{stock:, need:}
      when <~'ORE'> do
        def reaction: $ -> reactionMap::get;
        def amountNeeded: $ -> need::remove;
        def reactionsNeeded: ($amountNeeded + $reaction.result.amount - 1) ~/ $reaction.result.amount;
        $reaction.ingredients... ->
        \calculateNeed(def ingredient: $;
          def available: [ $ingredient.substance -> stock::remove ] -> \(when <=[]> do 0 ! when <> do $(1) !\);
          $available - $ingredient.amount * $reactionsNeeded ->
          \storeOrProcure(
            when <0~..> do { substance: $ingredient.substance, surplus: $ } -> stock::put -> !VOID
            when <..~0> do
              def deficit: -$;
              { substance: $ingredient.substance, deficit: $deficit }
              -> need::put // continues with old entry if one existed
              -> { substance: $.substance, deficit: $.deficit + $deficit }
              -> need::put -> !VOID
          \storeOrProcure) -> !VOID
        \calculateNeed) -> !VOID
        { substance: $, surplus: $reactionsNeeded * $reaction.result.amount - $amountNeeded } -> stock::put -> !VOID
end convertToIngredients

templates calculateOreForFuel&{stock:}
  def need: [{ substance: 'FUEL', deficit: $ }] -> hash/Map&{key: :(substance:), value: :(deficit:)};
  [$need::entries -> $.substance] -> #

  when <=['ORE']> do 'ORE' -> need::get !

  otherwise $... -> convertToIngredients&{stock: $stock, need: $need } -> !VOID
    [$need::entries -> $.substance] -> #
end calculateOreForFuel

// part 1
def oreForOneFuel: 1 -> calculateOreForFuel&{stock: [] -> hash/Map&{key: :(substance:), value: :(surplus:)}};
'$oreForOneFuel;
' -> !OUT::write

// part 2
source calculateFuelFromTrillion
  def trillion: 1000000000000;
  def stock: [] -> hash/Map&{key: :(substance:), value: :(surplus:)};
  @: 0;
  $trillion -> #
  $@ !

  when <$oreForOneFuel..> do
    def remaining: $;
    def fuelToMake: $ ~/ $oreForOneFuel;
    @: $@ + $fuelToMake;
    $fuelToMake -> calculateOreForFuel&{stock: $stock} -> $remaining - $ -> #

  otherwise
    def remaining: $;
    1 -> calculateOreForFuel&{stock: $stock} ->
      \(when <..$remaining> do @calculateFuelFromTrillion: $@calculateFuelFromTrillion + 1; $remaining - $ ! \) -> #
end calculateFuelFromTrillion

$calculateFuelFromTrillion -> !OUT::write
