include 'lib/list'

composer orbit
  [ <~'\)'> -> object´'$;' (<'\)'>) <object´'.*'> ]
end orbit

def orbitMap: [ $IN::lines -> orbit ];

templates allOrbits&{map:}
  @: [];
  [ {orbitter: $, orbits:[]} ] -> #
  when <[](0)> do $@ !
  otherwise
    def next: $(1);
    ..|@: $next;
    def orbits: [ $next.orbits..., $next.orbitter ];
    [ $(2..last)...,
      $map... -> \(when <?($(1) <=$next.orbitter>)> do $!\)
        -> { orbitter: $(2), orbits: $orbits} ] -> #
end allOrbits

def aroundCOM: object´'COM' -> allOrbits&{map: $orbitMap};

[ $aroundCOM... -> $.orbits::length ] -> list/sum -> !OUT::write
'
' -> !OUT::write

templates findPath
  def ends: $;
  def fromOrbits: $aroundCOM... -> \(when <{orbitter: <=$ends(1)>}> do $.orbits !\);
  def toOrbits: $aroundCOM... -> \(when <{orbitter: <=$ends(2)>}> do $.orbits !\);
  1 -> #

  when <?($fromOrbits($) <=$toOrbits($)>)> do
    $ + 1 -> #

  when <> do
    [ $fromOrbits(last..$:-1)..., $toOrbits($-1..last)... ] !
end findPath

[object´'YOU', object´'SAN'] -> findPath -> $::length - 1 -> !OUT::write
