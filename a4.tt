composer toInt
  <INT>
end toInt

templates valid
  def pw: $;
  def digits: ['$pw;'... -> toInt ];
  def diffs: $digits -> \[i](<?($i <2..>)> $ - $digits($i - 1) !\);
  $diffs -> \(<[<=0>]> $ !\) -> \(<~[<..~0>]> $ !\) -> $pw !
end valid

//part 1
def validPasswords: [ 284639..748759 -> valid ];
$validPasswords -> $::length -> !OUT::write

'
' -> !OUT::write

// part 2
templates runLengths
  def in: $;
  def breaks: [ $ -> \[i](<?($i <=1>)> 1 ! <~?($in($i) <=$in($i - 1)>)> $i !\)..., $in::length + 1];
  $breaks  -> \[i](<?($i <2..>)> $ - $breaks($i - 1) !\) !
end runLengths

[ $validPasswords... -> \( def pw: $;
  [ '$pw;'... ] -> runLengths -> \(<[<=2>]> $pw !\) !\) ] -> $::length -> !OUT::write

