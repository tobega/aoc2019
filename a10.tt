import 'lib/math'
import 'lib/hash'
import 'lib/list'

def map: [ $IN::lines -> [ $... ] ];

source maxAsteroidsDetectable
  @: { asteroids: 0 };
  $map -> \location[j, i](
    <='#'>
      @: 0;
      def linesOfSight: [] -> hash/Set;
      $map -> \eachAsteroid[y, x](
           <='#'?([$i,$j] <~=[$x,$y]>)>
               { dx: $x-$i, dy: $y-$j } -> \(
                  <{dx: <=0>, dy: <0..>}> { dx: 0, dy: 1} !
                  <{dx: <0..>, dy: <=0>}> { dx: 1, dy: 0} !
                  <{dx: <=0>}> { dx: 0, dy: -1} !
                  <{dy: <=0>}> { dx: -1, dy: 0} !
                  <>
                   def gcd: {a: $.dx, b: $.dy} -> math/gcd;
                   { dx: $.dx ~/ $gcd, dy: $.dy ~/ $gcd } !
               \) -> linesOfSight::add -> @location: $@location + $;
       \eachAsteroid) -> !VOID
       $@ -> \(<$@maxAsteroidsDetectable.asteroids..> @maxAsteroidsDetectable: { asteroids: $, x: $i, y: $j }; \) -> !VOID
  \location) -> !VOID
  $@ !
end maxAsteroidsDetectable

def station: $maxAsteroidsDetectable;
$station -> !OUT::write
'
' -> !OUT::write

//stereotype scannedPosition: <{direction: {quadrant:<1..4>, sin:<integer>, cos:<integer>}, hops: <integer>}>
templates processScan
  templates quadrant
    <{dy: <..~0>, dx: <0..>}> 1 !
    <{dy: <0..>, dx: <0~..>}> 2 !
    <{dy: <0~..>, dx: <..0>}> 3 !
    <{dy: <..0>, dx: <..~0>}> 4 !
  end quadrant
  def q: $ -> quadrant;
  def gcd: {a: $.dx, b: $.dy} -> math/gcd;
  { direction: { quadrant: $q,
      sin: $ -> \(<?($q <=1|3>)> $.dx ! <> $.dy !\) -> $ ~/ $gcd -> math/abs,
      cos: $ -> \(<?($q <=2|4>)> $.dx ! <> $.dy !\) -> $ ~/ $gcd -> math/abs},
    hops: $gcd } !
end processScan

templates scanOrder
  <?($(1).direction.quadrant <~=$(2).direction.quadrant>)>
    $(1).direction.quadrant - $(2).direction.quadrant !

  <?($(1).direction.sin * $(2).direction.cos <~=$(2).direction.sin * $(1).direction.cos>)>
    $(1).direction.sin * $(2).direction.cos - $(2).direction.sin * $(1).direction.cos !

  <> $(1).hops - $(2).hops !
end scanOrder

templates get200thShot
  @: { asteroids: $(2..-1), shot: 1, lastShot: $(1) };
  1 -> #
  <?($@.shot <=200>)> $@.lastShot !

  <$@.asteroids::length~..>
    @.lastShot: ^@.asteroids(1);
    @.shot: $@.shot + 1;
    1 -> #

  <?($@.asteroids($).direction <=$@.lastShot.direction>)> $ + 1 -> #

  <>
    @.lastShot: ^@.asteroids($);
    @.shot: $@.shot + 1;
    $ -> #
end get200thShot

$map -> \scanAsteroids[y, x](
     <='#'?([$x,$y] <~=[$station.x,$station.y]>)>
         { dx: $x-$station.x, dy: $y-$station.y } -> processScan
         -> { $..., x: $x, y: $y } !
 \scanAsteroids) -> list/flatten -> list/sort@{comparator: scanOrder}
 -> get200thShot -> ($.x - 1) * 100 + ($.y - 1) -> !OUT::write
