include 'aoc'
include 'lib/hash'

def botInterface: $IN::lines -> aoc/parseIntcode;

def directions: [ { move: 1"1", dX: 0"1", dY: -1"1"},
  { move: 2"1", dX: 0"1", dY: 1"1"},
  { move: 3"1", dX: -1"1", dY: 0"1"},
  { move: 4"1", dX: 1"1", dY: 0"1"}];

// Returns the shortest path to the target, or, when failed, the maximum number of steps possible
  templates findClosest&{map:, offset:, from:}
    def target: $;
    @:{ next:[], current:[{ position: $from, path: [] }], found: [],
      visited: $map -> \[i;j](0!\),
      steps: 0"1"
    };
    1 -> #
    when <?($@.found <[](1..)>)> do $@.found !
    when <?($@.current <[](1..)>)> do
      def base: ^@.current(1);
      $directions...
        -> { position: { x: $base.position.x + $.dX, y: $base.position.y + $.dY},
             path: [$base.path..., $.move]}
        -> \inBounds(when <?($.position.y + $offset.y <1..$map::length>)
                      ?($.position.x + $offset.x <1..$map(1)::length>)> do $ ! \inBounds)
        -> \canMove(when <?($map($.position.y + $offset.y; $.position.x + $offset.x)
                <=$target>)> do @findClosest.found: $.path;
             when <?($map($.position.y + $offset.y; $.position.x + $offset.x)
                <~='#'>)
              ?($@findClosest.visited($.position.y + $offset.y; $.position.x + $offset.x)
                <=0>)> do $ ! \canMove)
        -> \store(..|@findClosest.next: $;
             @findClosest.visited($.position.y + $offset.y; $.position.x + $offset.x): 1;\store)
        -> !VOID
      1 -> #
    when <?($@.next <[](1..)>)> do
      @.current: $@.next;
      @.next: [];
      @.steps: $@.steps + 1;
      1 -> #
    otherwise $@.steps !
  end findClosest

data coordinates <{ x: <"1">, y: <"1"> }>
data corridorMap <{map: <[](1.. //;1..
  )>, offset: <coordinates>, bot: <coordinates>, oxygenSystem: <={}|coordinates>}>
templates botExplore&{mode:}
  sink extendMapX
    when <=1> do
      @botExplore.map: $@botExplore.map -> \[i](['?', $...]!\);
      @botExplore.offset.x: $@botExplore.offset.x + 1;
    when <=$@botExplore.map(1)::length> do
      1..$@botExplore.map::length -> ..|@botExplore.map($): '?';
  end extendMapX
  sink extendMapY
    when <=1> do
      @botExplore.map: [[1..$@botExplore.map(1)::length -> '?'], $@botExplore.map...];
      @botExplore.offset.y: $@botExplore.offset.y + 1;
    when <=$@botExplore.map::length> do
      ..|@botExplore.map: [1..$@botExplore.map(1)::length -> '?'];
  end extendMapY

// Will follow currentPath if it exists, else find a new target.
// Returns nothing if a target cannot be found, which causes the bot to return a prompt '>'
// which then exits botExplore
  source getNextMove
    1 -> #
    $@botExplore.currentPath -> \(<[](1..)> $(1)! \) !

    when <?($@botExplore.currentPath <[](1..)>)> do !VOID
    when <?($@botExplore.oxygenSystem <=$@botExplore.bot>) ?($mode <='find'>)> do
      // find a potential shortest path back to bot start
      'B' -> findClosest&{map: $@botExplore.map, offset: $@botExplore.offset, from: $@botExplore.bot}
          -> ..|@botExplore: {solution: $, currentPath: $};
    when <?($@botExplore.oxygenSystem <{x:<>}>) ?($mode <='find'>)> do
      // go back to oxygen system and find a new shortest path
      'O' -> findClosest&{map: $@botExplore.map, offset: $@botExplore.offset, from: $@botExplore.bot} -> @botExplore.currentPath: $;
    otherwise
      // Just find the closest unexplored square
      '?' -> findClosest&{map: $@botExplore.map, offset: $@botExplore.offset, from: $@botExplore.bot}
        -> \(<[]> $! \) -> @botExplore.currentPath: $;
  end getNextMove

  @: { map: [['?', '?', '?'], ['?', 'B', '?'], ['?', '?', '?']],
    offset: { x: 2, y: 2},
    oxygenSystem: {},
    $..., // overwrites previous if given
    bot: { x: 0, y: 0},
    currentPath: []
  };
  def bot: $botInterface -> aoc/MarkII&{cooperativeThreading: 1};
  [$getNextMove] -> bot::proceed -> #

  when <{out: <=2>} ?($mode <='find'>)> do
    def move: $directions(^@.currentPath(1));
    def mapX: $@.bot.x + $@.offset.x + $move.dX;
    def mapY: $@.bot.y + $@.offset.y + $move.dY;
    @.map($mapY; $mapX): 'O';
    @.bot.x: $@.bot.x + $move.dX;
    @.bot.y: $@.bot.y + $move.dY;
    @.oxygenSystem: $@.bot;
    // Still extend for the next phase.
    $mapX -> !extendMapX
    $mapY -> !extendMapY
    [$getNextMove] -> bot::proceed -> #
  when <{out: <=0>}> do
    def move: $directions(^@.currentPath(1));
    def mapX: $@.bot.x + $@.offset.x + $move.dX;
    def mapY: $@.bot.y + $@.offset.y + $move.dY;
    @.map($mapY; $mapX): '#';
    $mapX -> !extendMapX
    $mapY -> !extendMapY
    @.currentPath: [];
    [ $getNextMove ] -> bot::proceed -> #
  when <{out: <=1|=2>}> do
    def move: $directions(^@.currentPath(1));
    def mapX: $@.bot.x + $@.offset.x + $move.dX;
    def mapY: $@.bot.y + $@.offset.y + $move.dY;
    // Mark as open if previously unknown
    $@.map($mapY; $mapX) -> \(<='?'> $!\) -> @.map($mapY; $mapX): '.';
    @.bot.x: $@.bot.x + $move.dX;
    @.bot.y: $@.bot.y + $move.dY;
    $mapX -> !extendMapX
    $mapY -> !extendMapY
    $@.bot -> \moveIfNotDone(when <={x: 0, y: 0}?($@botExplore.solution <>)> do 99 !
      otherwise [ $getNextMove ] -> bot::proceed ! \moveIfNotDone) -> #
  otherwise
    $@ !
end botExplore

// part 1
def mapFoundOxygen: {} -> botExplore&{mode: 'find'};
$mapFoundOxygen -> $.solution::length -> !OUT::write
'
' -> !OUT::write
// part 2
def fullMap: $mapFoundOxygen -> botExplore&{mode: 'fill'};
'Z' -> findClosest&{map: $fullMap.map, offset: $fullMap.offset, from: $fullMap.oxygenSystem} -> !OUT::write
