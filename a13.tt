include 'aoc'
include 'lib/list'
include 'lib/math'

def game: $IN::lines -> aoc/parseIntcode;

templates arcadeCabinet
  def engine: $ -> aoc/MarkII&{cooperativeThreading: 1};
  @: { screen: 0"y":[], output: [], done: 0 };
  [] -> engine::proceed -> #
  '$@.screen... -> '$... -> \(<=0"1"> ' '! <=1"1"> '█'! <=2"1"> '░'! <=3"1"> '_'! <=4"1"> '*'!\);$#10;';$#10;' -> !OUT::write
  $@.screen !
  when <{status: <='end'>}> do
    @.done: 1;
    ..|@.output: $.out;
    $@.output -> #
  when <{out: <>}> do
    ..|@.output: $.out;
    $@.output -> #
  when <[](3..)> do
    def x: (^@.output(1))"x";
    def y: (^@.output(1))"y";
    ($@.screen::length)"y"..$y -> ..|@.screen: 0"x":[];
    ($@.screen($y)::length)"x"..$x -> ..|@.screen($y): 0;
    @.screen($y;$x): ^@.output(1);
    [] -> \(<?($@arcadeCabinet.done <=done´0>)> $ !\) -> engine::proceed -> #
  when <[]> do
    [] -> \(<?($@arcadeCabinet.done <=done´0>)> $ !\) -> engine::proceed -> #
  otherwise 'Error $;' -> !OUT::write
end arcadeCabinet

// part 1
$game -> arcadeCabinet -> list/flatten -> [ $... -> \(<=2"1"> $ !\) ] -> $::length -> !OUT::write
'
' -> !OUT::write

templates botPlayer
  @: { ballX: -1"1", paddleX: -1"1", score: -1"1", output: [], done: 0 };
  def engine: [2"1", $(2..last)...] -> aoc/MarkII&{cooperativeThreading: 1};
  [] -> engine::proceed -> #
  $@.score !
  when <='>'> do
    [ $@.ballX - $@.paddleX -> math/sign ] -> engine::proceed -> #
  when <{status: <='end'>}> do
    @.done: 1;
    ..|@.output: $.out;
    $@.output -> #
  when <{out: <>}> do
    ..|@.output: $.out;
    $@.output -> #
  when <[](3..)?($(1) <=-1"1">)?($(2) <=0"1">)> do
    @.score: $(3);
    @.output: $@.output(4..last);
    [] -> \(<?($@botPlayer.done <=done´0>)> $ !\) -> engine::proceed -> #
  when <[](3..)?($(3) <=3"1">)> do
    @.paddleX: $(1);
    @.output: $@.output(4..last);
    [] -> \(<?($@botPlayer.done <=done´0>)> $ !\) -> engine::proceed -> #
  when <[](3..)?($(3) <=4"1">)> do
    @.ballX: $(1);
    @.output: $@.output(4..last);
    [] -> \(<?($@botPlayer.done <=done´0>)> $ !\) -> engine::proceed -> #
  when <[](3..)> do
    @.output: $@.output(4..last);
    [] -> \(<?($@botPlayer.done <=done´0>)> $ !\) -> engine::proceed -> #
  when <[]> do
    [] -> \(<?($@botPlayer.done <=done´0>)> $ !\) -> engine::proceed -> #
  otherwise 'Error $;' -> !OUT::write
end botPlayer

// part 2
$game -> botPlayer -> !OUT::write
